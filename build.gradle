plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "net.raphimc.class-token-replacer" version "1.1.3"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    group = project.maven_group ?: rootProject.maven_group
    archivesName = project.maven_name ?: rootProject.maven_name
    version = project.maven_version ?: rootProject.maven_version
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly(annotationProcessor("org.projectlombok:lombok:1.18.34"))
    compileOnly "org.jetbrains:annotations:24.1.0"

    api "com.google.code.gson:gson:2.11.0"
    api "io.jsonwebtoken:jjwt-impl:0.12.6"
    api("io.jsonwebtoken:jjwt-gson:0.12.6") {
        exclude group: "com.google.code.gson", module: "gson"
    }
    api "org.slf4j:slf4j-api:2.0.15"
}

sourceSets {
    main {
        classTokenReplacer {
            property("\${version}", project.version)
            property("\${impl_version}", "git-${project.name}-${project.version}:${project.latestCommitHash()}")
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.name ?: rootProject.name}" }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.maven_name
            version = project.maven_version

            from components.java

            pom {
                name = "MinecraftAuth"
                description = "Simple and easy to use Minecraft microsoft authentication library (Java and Bedrock)"
                url = "https://github.com/RaphiMC/MinecraftAuth"
                licenses {
                    license {
                        name = "LGPL-3.0 License"
                        url = "https://github.com/RaphiMC/MinecraftAuth/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/RaphiMC/MinecraftAuth.git"
                    developerConnection = "scm:git:ssh://github.com/RaphiMC/MinecraftAuth.git"
                    url = "https://github.com/RaphiMC/MinecraftAuth.git"
                }
            }
        }
    }
}

String latestCommitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
